(pd
  (defs
    (IFNγ-dimer-c macromolecule-multimer name:"IFNγ" uoi:N:2)
    (IFNγR1-dimer-c macromolecule-multimer name:"IFNγR1" uoi:N:2 Y457:<f>)
    (IFNγR1-dimer-P-c macromolecule-multimer name:"IFNγR1" uoi:N:2 Y457:P)
    (IFNγR2-dimer-c macromolecule-multimer name:"IFNγR2" uoi:N:2)
    (JAK1-c macromolecule name:"JAK1")
    (JAK2-c macromolecule name:"JAK2")
    (CaM-dimer macromolecule-multimer name:"CaM" uoi:N:2)
    (INOS-dimer macromolecule-multimer name:"INOS" uoi:N:2))
  (EXTRACELLULAR_SPACE:compartment
    (IFNγ macromolecule-multimer uoi:N:2)
    (arginine-extracellular simple-chemical)
    (NO-extracellular simple-chemical name:"NO"))
  (PLASMA_MEMBRANE:compartment
    (c1 complex-multimer (IFNγR1-dimer-c IFNγR2-dimer-c JAK1-c JAK2-c))
    (c2 complex-multimer (IFNγ-dimer-c IFNγR1-dimer-c IFNγR2-dimer-c JAK1-c JAK2-c))
    (c3 complex-multimer (IFNγ-dimer-c IFNγR1-dimer-P-c IFNγR2-dimer-c JAK1-c JAK2-c)))
  (CYTOSOL:compartment
    (STAT1α macromolecule Y701:<f> S727:<f>)
    (STAT1α-P-P macromolecule name:"STAT1α" Y701:P S727:P)
    (STAT1α-P-P-dimer macromolecule-multimer name:"STAT1α" uoi:N:2 Y701:P S727:P)
    (s1 source)
    (IRF1-mRNA nucleic-acid-feature name:"IRF1" uoi:ct:mRNA)
    (s2 sink)
    (IRF1 macromolecule)
    (INOS-mRNA nucleic-acid-feature name:"INOS" uoi:ct:mRNA)
    (s3 source)
    (INOS macromolecule)
    (s4 sink)
    (CaM macromolecule)
    (c4 complex-multimer (CaM-dimer INOS-dimer))
    (NO simple-chemical)
    (citrulline simple-chemical)
    (aspartate simple-chemical)
    (arginino-succinate simple-chemical)
    (fumarate simple-chemical)
    (arginine simple-chemical)
    (O2 simple-chemical)
    (ASL macromolecule-multimer uoi:N:4)
    (ASS macromolecule))
  (NUCLEUS:compartment
    (STAT1α-P-P-dimer-nucleus macromolecule-multimer name:"STAT1α" uoi:N:2 Y701:P S727:P)
    (IRF1-gene nucleic-acid-feature name:"IRF1" uoi:ct:gene)
    (s5 source)
    (IRF1-mRNA-nucleus nucleic-acid-feature name:"IRF1" uoi:ct:mRNA)
    (IRF1-nucleus macromolecule name:"IRF1")
    (INOS-gene nucleic-acid-feature name:"INOS" uoi:ct:gene)
    (s6 source)
    (INOS-mRNA-nucleus nucleic-acid-feature name:"INOS" uoi:ct:mRNA)
    
    ((IFNγ c1) process c2)
    (c2 (process p1) c3)
    (c2 stimulation p1)
    (STAT1α (process p2) STAT1α-P-P)
    (c3 stimulation p2)
    (STAT1α-P-P process STAT1α-P-P-dimer)
    (STAT1α-P-P-dimer process STAT1α-P-P-dimer-nucleus)
    (s5 (process p3) IRF1-mRNA-nucleus)
    ((and STAT1α-P-P-dimer-nucleus IRF1-gene) necessary-stimulation p3)
    (IRF1-mRNA-nucleus process IRF1-mRNA)
    (IRF1-mRNA process s2)
    (s1 (process p4) IRF1)
    (IRF1-mRNA necessary-stimulation p4)
    (IRF1 process IRF1-nucleus)
    (s6 (process p5) INOS-mRNA-nucleus)
    ((and IRF1-nucleus INOS-gene) necessary-stimulation p5)
    (INOS-mRNA-nucleus process INOS-mRNA)
    (INOS-mRNA process s4)
    (s5 (process p6) INOS)
    (INOS-mRNA necessary-stimulation p6)
    ((INOS CaM) process c4)
    ((arginine O2) (process p7) (NO citrulline))
    (c4 catalysis p7)
    (arginine-extracellular process arginine)
    (NO process NO-extracellular)
    ((citrulline aspartate) (process p8) arginino-succinate)
    (ASS catalysis p8)
    (arginino-succinate (process p9) (fumarate arginine))
    (ASL catalysis p9)))
    

